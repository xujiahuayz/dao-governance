{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "8b741e43",
   "metadata": {},
   "source": [
    "# The Determinants of Small Shareholder Victory"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 15,
   "id": "b142d515",
   "metadata": {},
   "outputs": [],
   "source": [
    "import stata_setup\n",
    "\n",
    "stata_setup.config('/Applications/StataNow', 'mp')"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 16,
   "id": "2eb31b95",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%stata\n",
    "\n",
    "clear all\n",
    "set more off\n",
    "set varabbrev off\n",
    "version 18\n",
    "\n",
    "* Paths\n",
    "global PROCESSED_DATA \"processed_data\"\n",
    "global TABLES \"tables\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "id": "64d9a996",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%stata\n",
    "\n",
    "import delimited using \"$PROCESSED_DATA/proposals_panel.csv\", clear\n",
    "\n",
    "* Parse date\n",
    "gen day = date(date, \"YMD\")\n",
    "format day %td\n",
    "gen month = month(day)\n",
    "gen quarter = quarter(day)\n",
    "\n",
    "gen year = year(day)\n",
    "encode gecko_id, gen(token)\n",
    "encode space,    gen(dao)\n",
    "\n",
    "* Dependent variables\n",
    "local wins non_whale_victory_vn non_whale_victory_vp non_whale_victory_vp_vn\n",
    "\n",
    "* Independent variables\n",
    "local proposal_char multi_choices weighted quorum have_discussion delegation\n",
    "local delegate_char whale_from_delegation_rate non_whale_from_delegation_rate ///\n",
    "                    whale_to_delegation_rate non_whale_to_delegation_rate\n",
    "local vote_char whale_vs_hhi non_whale_vs_hhi whale_vn_hhi non_whale_vn_hhi\n",
    "\n",
    "* Label variables\n",
    "label var non_whale_victory_vn \"\\${\\it Small Win}^{vn}_{i,t}\\$\"\n",
    "label var non_whale_victory_vp \"\\${\\it Small Win}^{vp}_{i,t}\\$\"\n",
    "label var non_whale_victory_vp_vn \"\\${\\it Small Win}^{vp,vn}_{i,t}\\$\"\n",
    "label var weighted \"\\${\\it Weighted}_{i,t}\\$\"\n",
    "label var quorum \"\\${\\it Quorum}_{i,t}\\$\"\n",
    "label var multi_choices \"\\${\\it Multiple Choices}_{i,t}\\$\"\n",
    "label var have_discussion \"\\${\\it Discussion}_{i,t}\\$\"\n",
    "label var delegation \"\\${\\it Delegation}_{i,t}\\$\"\n",
    "label var non_whale_from_delegation_rate \"\\$\\textit{Delegator}_{i,t}^{\\textit{Small}}\\$\"\n",
    "label var whale_from_delegation_rate \"\\$\\textit{Delegator}_{i,t}^{\\textit{Whale}}\\$\"\n",
    "label var non_whale_to_delegation_rate \"\\$\\textit{Delegatee}_{i,t}^{\\textit{Small}}\\$\"\n",
    "label var whale_to_delegation_rate \"\\$\\textit{Delegatee}_{i,t}^{\\textit{Whale}}\\$\"\n",
    "label var non_whale_vs_hhi \"\\$\\textit{HHI}_{i,t}^{\\textit{Small,vs}}\\$\"\n",
    "label var whale_vs_hhi \"\\$\\textit{HHI}_{i,t}^{\\textit{Whale,vs}}\\$\"\n",
    "label var non_whale_vn_hhi \"\\$\\textit{HHI}_{i,t}^{\\textit{Small,vn}}\\$\"\n",
    "label var whale_vn_hhi \"\\$\\textit{HHI}_{i,t}^{\\textit{Whale,vn}}\\$\""
   ]
  },
  {
   "cell_type": "markdown",
   "id": "92ea77f8",
   "metadata": {},
   "source": [
    "## Summary Statistics"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 18,
   "id": "ec193731",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%stata\n",
    "\n",
    "******************************************************\n",
    "* SUMMARY STATISTICS (before regression)\n",
    "******************************************************\n",
    "* Summarize all key variables\n",
    "summarize `wins' `proposal_char' `delegate_char' `vote_char', detail\n",
    "\n",
    "* Export a nice LaTeX summary table with mean, SD, p10, median, p90\n",
    "estpost summarize `wins' `proposal_char' `delegate_char' `vote_char', detail\n",
    "esttab using \"$TABLES/summary_stats.tex\", replace ///\n",
    "    cells(\"count(fmt(0)) mean(fmt(3)) sd(fmt(3)) p10(fmt(3)) p50(fmt(3)) p90(fmt(3))\") ///\n",
    "    collabels(\"Number of Obs.\" \"Mean\" \"Std. Dev.\" \"P10\" \"Median\" \"P90\") ///\n",
    "    nonumber nomtitles noobs label ///\n",
    "    alignment(D{.}{.}{-1}) booktabs ///\n",
    "    compress nofloat nostar ///\n",
    "    substitute(\"\\_\" \"_\")                     ///\n",
    "    title(\"Summary Statistics of Variables\") ///\n",
    "    prehead(\"\\begin{tabular}{l*{6}{c}}\\toprule\") ///\n",
    "    posthead(\"\\hline\") ///\n",
    "    postfoot(\"\\bottomrule\\end{tabular}\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "32b80114",
   "metadata": {},
   "source": [
    "## Proposal Characteristics"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 19,
   "id": "f4f1f810",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%stata\n",
    "\n",
    "******************************************************\n",
    "* LOGISTIC REGRESSIONS\n",
    "******************************************************\n",
    "\n",
    "eststo clear\n",
    "\n",
    "foreach y of local wins {\n",
    "    * Logit regression with SEs clustered by DAO\n",
    "    logit `y' `proposal_char', vce(cluster dao)\n",
    "    eststo `y'_p\n",
    "}\n",
    "\n",
    "* Export LaTeX table\n",
    "esttab                                                     ///\n",
    "    non_whale_victory_vn_p                                 ///\n",
    "    non_whale_victory_vp_p                                 ///\n",
    "    non_whale_victory_vp_vn_p                              ///\n",
    "    using \"$TABLES/reg_logit_win_proposal.tex\", replace    ///\n",
    "    se star(* 0.10 ** 0.05 *** 0.01)                       ///\n",
    "    b(%9.3f) se(%9.2f)                                     ///\n",
    "    label nogaps booktabs nonotes noomitted eqlabels(none) ///\n",
    "    mtitles(\"\\${\\it Small Win}^{vn}_{i,t}\\$\"               ///\n",
    "    \"\\${\\it Small Win}^{vp}_{i,t}\\$\"                       ///  \n",
    "    \"\\${\\it Small Win}^{vp,vn}_{i,t}\\$\")                   ///\n",
    "    substitute(\"\\_\" \"_\")                                   ///\n",
    "    stats(N r2_p,                                          ///\n",
    "        fmt(%9.0fc %9.3f)                                  ///\n",
    "        labels(\"Observations\" \"Pseudo R²\"))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1ca5fd8f",
   "metadata": {},
   "source": [
    "## Delegation Characteristics"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 22,
   "id": "4919e064",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%stata\n",
    "\n",
    "******************************************************\n",
    "* LOGISTIC REGRESSIONS\n",
    "******************************************************\n",
    "\n",
    "eststo clear\n",
    "\n",
    "foreach y of local wins {\n",
    "    * Logit regression with SEs clustered by DAO\n",
    "    logit `y' whale_from_delegation_rate non_whale_from_delegation_rate, vce(cluster dao)\n",
    "    eststo `y'_f\n",
    "\n",
    "    logit `y' whale_to_delegation_rate non_whale_to_delegation_rate, vce(cluster dao)\n",
    "    eststo `y'_t\n",
    "}\n",
    "\n",
    "* Export LaTeX table\n",
    "esttab                                                      ///\n",
    "    non_whale_victory_vn_f                                  ///\n",
    "    non_whale_victory_vn_t                                  ///\n",
    "    non_whale_victory_vp_f                                  ///\n",
    "    non_whale_victory_vp_t                                  ///\n",
    "    non_whale_victory_vp_vn_f                               ///\n",
    "    non_whale_victory_vp_vn_t                               ///\n",
    "    using \"$TABLES/reg_logit_win_delegate.tex\", replace     ///\n",
    "    se star(* 0.10 ** 0.05 *** 0.01)                        ///\n",
    "    b(%9.3f) se(%9.2f)                                      ///\n",
    "    label nogaps booktabs nonotes noomitted eqlabels(none)  ///\n",
    "    mgroups(\"\\${\\it Small Win}^{vn}_{i,t}\\$\"                ///\n",
    "    \"\\${\\it Small Win}^{vp}_{i,t}\\$\"                        ///  \n",
    "    \"\\${\\it Small Win}^{vp,vn}_{i,t}\\$\",                    ///\n",
    "    span                                                    ///\n",
    "    pattern(1 0 1 0 1 0)                                    ///\n",
    "    prefix(\\multicolumn{@span}{c}{)                         ///\n",
    "    suffix(})                                               ///\n",
    "    erepeat(\\cmidrule(lr){@span}) )                         ///\n",
    "    substitute(\"\\_\" \"_\")                                    ///\n",
    "    stats(N r2_p,                                           ///\n",
    "        fmt(%9.0fc %9.3f)                                   ///\n",
    "        labels(\"Observations\" \"Pseudo R²\"))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2e6f480d",
   "metadata": {},
   "source": [
    "## Voting Characteristics"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 23,
   "id": "684cecfd",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%stata\n",
    "\n",
    "******************************************************\n",
    "* LOGISTIC REGRESSIONS\n",
    "******************************************************\n",
    "\n",
    "eststo clear\n",
    "\n",
    "logit non_whale_victory_vn whale_vn_hhi non_whale_vn_hhi \n",
    "eststo non_whale_victory_vn_h\n",
    "\n",
    "logit non_whale_victory_vp whale_vs_hhi non_whale_vs_hhi\n",
    "eststo non_whale_victory_vp_h\n",
    "\n",
    "logit non_whale_victory_vp_vn whale_vs_hhi non_whale_vn_hhi \n",
    "eststo non_whale_victory_vp_vn_h\n",
    "\n",
    "\n",
    "* Export LaTeX table\n",
    "esttab                                                     ///\n",
    "    non_whale_victory_vn_h                                 ///\n",
    "    non_whale_victory_vp_h                                 ///\n",
    "    non_whale_victory_vp_vn_h                              ///\n",
    "    using \"$TABLES/reg_logit_win_vote.tex\", replace    ///\n",
    "    se star(* 0.10 ** 0.05 *** 0.01)                       ///\n",
    "    b(%9.3f) se(%9.2f)                                     ///\n",
    "    label nogaps booktabs nonotes noomitted eqlabels(none) ///\n",
    "    mtitles(\"\\${\\it Small Win}^{vn}_{i,t}\\$\"               ///\n",
    "    \"\\${\\it Small Win}^{vp}_{i,t}\\$\"                       ///  \n",
    "    \"\\${\\it Small Win}^{vp,vn}_{i,t}\\$\")                   ///\n",
    "    substitute(\"\\_\" \"_\")                                   ///\n",
    "    stats(N r2_p,                                          ///\n",
    "        fmt(%9.0fc %9.3f)                                  ///\n",
    "        labels(\"Observations\" \"Pseudo R²\"))"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "venv",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.12.10"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
