{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "ba61e113",
   "metadata": {},
   "source": [
    "# T-Test Table"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "4cff6f46",
   "metadata": {},
   "outputs": [],
   "source": [
    "import stata_setup\n",
    "\n",
    "stata_setup.config('/Applications/StataNow', 'mp')"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "ee6e1a78",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      ". /****************************************************************************\n",
      "> **************\n",
      "> * File: compare_whale_vs_smallholders_fullstats.do\n",
      "> * Goal: Small vs Whale for num, turnout:\n",
      "> *       Mean, SD, P10, Median, P90 (each group) + Diff (means) + t-stat (star\n",
      "> s)\n",
      "> * Notes: # Holders -> 1 dp; Turnout -> 3 dp\n",
      "> *****************************************************************************\n",
      "> *************/\n",
      ". \n",
      ". clear all\n",
      "\n",
      ". set more off\n",
      "\n",
      ". set varabbrev off\n",
      "\n",
      ". global PROCESSED_DATA \"processed_data\"\n",
      "\n",
      ". global TABLES \"tables\"\n",
      "\n",
      ". \n",
      ". * -------------------- LOAD DATA --------------------\n",
      ". import delimited \"$PROCESSED_DATA/proposals_voter_user.csv\", clear varn(1) ca\n",
      "> se(lower)\n",
      "(encoding automatically selected: ISO-8859-1)\n",
      "(39 vars, 2,681 obs)\n",
      "\n",
      ". capture destring whale* non_whale*, replace force\n",
      "\n",
      ". \n",
      ". local metrics num_delegate turnout_delegate from_delegation_rate to_delegatio\n",
      "> n_rate reason_rate vn_hhi timing_avg user\n",
      "\n",
      ". \n",
      ". * -------------------- COLLECT RESULTS --------------------\n",
      ". tempfile results\n",
      "\n",
      ". postfile H str80(metric) ///\n",
      ">     double( ///\n",
      ">         ms_mean ms_sd ms_p10 ms_p50 ms_p90 ///\n",
      ">         mw_mean mw_sd mw_p10 mw_p50 mw_p90 ///\n",
      ">         diff_mean tstat pval ///\n",
      ">     ) str3(stars) using `results', replace\n",
      "(file /var/folders/1n/px68gf0s0d30s0p5010q1_y40000gn/T//St08889.000006 not\n",
      "    found)\n",
      "\n",
      ". \n",
      ". foreach m of local metrics {\n",
      "  2.     local whale whale_`m'\n",
      "  3.     local small non_whale_`m'\n",
      "  4. \n",
      ".     * Small (non-whale) stats\n",
      ".     quietly summarize `small', detail\n",
      "  5.     local ms_mean = r(mean)\n",
      "  6.     local ms_sd   = r(sd)\n",
      "  7.     local ms_p10  = r(p10)\n",
      "  8.     local ms_p50  = r(p50)\n",
      "  9.     local ms_p90  = r(p90)\n",
      " 10. \n",
      ".     * Whale stats\n",
      ".     quietly summarize `whale', detail\n",
      " 11.     local mw_mean = r(mean)\n",
      " 12.     local mw_sd   = r(sd)\n",
      " 13.     local mw_p10  = r(p10)\n",
      " 14.     local mw_p50  = r(p50)\n",
      " 15.     local mw_p90  = r(p90)\n",
      " 16. \n",
      ".     * Difference in means with t-test\n",
      ".     quietly gen __diff = `small' - `whale'\n",
      " 17. \n",
      ".     quietly reg __diff, vce(cluster space)\n",
      " 18. \n",
      ".     local d  = _b[_cons]\n",
      " 19.     local tt = _b[_cons] / _se[_cons]\n",
      " 20.     local p  = 2 * ttail(e(df_r), abs(`tt'))\n",
      " 21. \n",
      ".     drop __diff\n",
      " 22. \n",
      ". \n",
      ".     * Stars\n",
      ".     if (`p' < 0.01)      local stars \"***\"\n",
      " 23.     else if (`p' < 0.05) local stars \"**\"\n",
      " 24.     else if (`p' < 0.10) local stars \"*\"\n",
      " 25.     else                 local stars \"\"\n",
      " 26. \n",
      ".     * Row label\n",
      ".     if \"`m'\" == \"num_delegate\"         local label \"\\$\\textit{\\# Holders}_{i}\n",
      "> \\$\"\n",
      " 27.     else if \"`m'\" == \"turnout_delegate\"  local label \"\\$\\textit{Turnout}_{\n",
      "> i}\\$\"\n",
      " 28.     else if \"`m'\" == \"from_delegation_rate\"  local label \"\\$\\textit{Delega\n",
      "> tor}_{i}\\$\"\n",
      " 29.     else if \"`m'\" == \"to_delegation_rate\"    local label \"\\$\\textit{Delega\n",
      "> tee}_{i}\\$\"\n",
      " 30.     else if \"`m'\" == \"reason_rate\"    local label \"\\$\\textit{Reason}_{i}\\$\n",
      "> \"\n",
      " 31.     else if \"`m'\" == \"vn_hhi\"    local label \"\\$\\textit{HHI}_{i}^{vn}\\$\"\n",
      " 32.     else if \"`m'\" == \"timing_avg\"    local label \"\\$\\textit{Timing}_{i}\\$\"\n",
      " 33.     else if \"`m'\" == \"user\"    local label \"\\$\\textit{User}_{i}\\$\"\n",
      " 34.     else                      local label \"`m'\"\n",
      " 35. \n",
      ".     post H (\"`label'\") ///\n",
      ">         (`ms_mean') (`ms_sd') (`ms_p10') (`ms_p50') (`ms_p90') ///\n",
      ">         (`mw_mean') (`mw_sd') (`mw_p10') (`mw_p50') (`mw_p90') ///\n",
      ">         (`d') (`tt') (`p') (\"`stars'\")\n",
      " 36. }\n",
      "\n",
      ". postclose H\n",
      "\n",
      ". \n",
      ". * -------------------- LATEX EXPORT --------------------\n",
      ". use `results', clear\n",
      "\n",
      ". \n",
      ". tempname fh\n",
      "\n",
      ". file open `fh' using \"$TABLES/sum_whale_non_whale.tex\", write replace\n",
      "\n",
      ". \n",
      ". file write `fh' \"{\\small\" _n\n",
      "\n",
      ". file write `fh' \"\\begin{tabular}{lcccccccccccc}\" _n\n",
      "\n",
      ". file write `fh' \"\\toprule\" _n\n",
      "\n",
      ". file write `fh' \" & \\multicolumn{5}{c}{Small Shareholder (Holding \\$<\\$ 5\\%)}\n",
      ">  & \\multicolumn{5}{c}{Whale (Holding \\$\\ge\\$ 5\\%)} & \\multicolumn{2}{c}{Small\n",
      ">  - Whale} \\\\\" _n\n",
      "\n",
      ". file write `fh' \"\\cmidrule(lr){2-6} \\cmidrule(lr){7-11} \\cmidrule(lr){12-13}\"\n",
      ">  _n\n",
      "\n",
      ". file write `fh' \" & Mean & Std. Dev. & P10 & Median & P90 & Mean & Std. Dev. \n",
      "> & P10 & Median & P90 & Diff. & t-stat \\\\\" _n\n",
      "\n",
      ". file write `fh' \"\\midrule\" _n\n",
      "\n",
      ". \n",
      ". local Nobs = _N\n",
      "\n",
      ". forvalues i = 1/`Nobs' {\n",
      "  2.     local metric = metric[`i']\n",
      "  3. \n",
      ".     * Precision rules\n",
      ".     if \"`metric'\" == \"\\$\\textit{\\# Holders}_{i}\\$\" {\n",
      "  4.         local ms_mean_fmt : display %12.1f ms_mean[`i']\n",
      "  5.         local ms_sd_fmt   : display %12.1f ms_sd[`i']\n",
      "  6.         local ms_p10_fmt  : display %12.1f ms_p10[`i']\n",
      "  7.         local ms_p50_fmt  : display %12.1f ms_p50[`i']\n",
      "  8.         local ms_p90_fmt  : display %12.1f ms_p90[`i']\n",
      "  9. \n",
      ".         local mw_mean_fmt : display %12.1f mw_mean[`i']\n",
      " 10.         local mw_sd_fmt   : display %12.1f mw_sd[`i']\n",
      " 11.         local mw_p10_fmt  : display %12.1f mw_p10[`i']\n",
      " 12.         local mw_p50_fmt  : display %12.1f mw_p50[`i']\n",
      " 13.         local mw_p90_fmt  : display %12.1f mw_p90[`i']\n",
      " 14. \n",
      ".         local diff_fmt    : display %12.1f diff_mean[`i']\n",
      " 15.         local tstat_fmt   : display %12.1f tstat[`i']\n",
      " 16.     }\n",
      " 17.     else {\n",
      " 18.         * Fallback (3 dp)\n",
      ".         local ms_mean_fmt : display %12.4f ms_mean[`i']\n",
      " 19.         local ms_sd_fmt   : display %12.4f ms_sd[`i']\n",
      " 20.         local ms_p10_fmt  : display %12.4f ms_p10[`i']\n",
      " 21.         local ms_p50_fmt  : display %12.4f ms_p50[`i']\n",
      " 22.         local ms_p90_fmt  : display %12.4f ms_p90[`i']\n",
      " 23. \n",
      ".         local mw_mean_fmt : display %12.4f mw_mean[`i']\n",
      " 24.         local mw_sd_fmt   : display %12.4f mw_sd[`i']\n",
      " 25.         local mw_p10_fmt  : display %12.4f mw_p10[`i']\n",
      " 26.         local mw_p50_fmt  : display %12.4f mw_p50[`i']\n",
      " 27.         local mw_p90_fmt  : display %12.4f mw_p90[`i']\n",
      " 28. \n",
      ".         local diff_fmt    : display %12.4f diff_mean[`i']\n",
      " 29.         local tstat_fmt   : display %12.4f tstat[`i']\n",
      " 30.     }\n",
      " 31. \n",
      ".     local st = stars[`i']\n",
      " 32.     file write `fh' ///\n",
      ">         \"`metric' & `ms_mean_fmt' & `ms_sd_fmt' & `ms_p10_fmt' & `ms_p50_fmt'\n",
      ">  & `ms_p90_fmt' & \" ///\n",
      ">         \"`mw_mean_fmt' & `mw_sd_fmt' & `mw_p10_fmt' & `mw_p50_fmt' & `mw_p90_\n",
      "> fmt' & \" ///\n",
      ">         \"`diff_fmt' & `tstat_fmt'`st' \\\\\" _n\n",
      " 33. }\n",
      "\n",
      ". \n",
      ". file write `fh' \"\\bottomrule\" _n \"\\end{tabular}}\" _n\n",
      "\n",
      ". file close `fh'\n",
      "\n",
      ". \n"
     ]
    }
   ],
   "source": [
    "%%stata\n",
    "/******************************************************************************************\n",
    "* File: compare_whale_vs_smallholders_fullstats.do\n",
    "* Goal: Small vs Whale for num, turnout:\n",
    "*       Mean, SD, P10, Median, P90 (each group) + Diff (means) + t-stat (stars)\n",
    "* Notes: # Holders -> 1 dp; Turnout -> 3 dp\n",
    "******************************************************************************************/\n",
    "\n",
    "clear all\n",
    "set more off\n",
    "set varabbrev off\n",
    "global PROCESSED_DATA \"processed_data\"\n",
    "global TABLES \"tables\"\n",
    "\n",
    "* -------------------- LOAD DATA --------------------\n",
    "import delimited \"$PROCESSED_DATA/proposals_voter_user.csv\", clear varn(1) case(lower)\n",
    "capture destring whale* non_whale*, replace force\n",
    "\n",
    "local metrics num_delegate turnout_delegate from_delegation_rate to_delegation_rate reason_rate vn_hhi timing_avg user\n",
    "\n",
    "* -------------------- COLLECT RESULTS --------------------\n",
    "tempfile results\n",
    "postfile H str80(metric) ///\n",
    "    double( ///\n",
    "        ms_mean ms_sd ms_p10 ms_p50 ms_p90 ///\n",
    "        mw_mean mw_sd mw_p10 mw_p50 mw_p90 ///\n",
    "        diff_mean tstat pval ///\n",
    "    ) str3(stars) using `results', replace\n",
    "\n",
    "foreach m of local metrics {\n",
    "    local whale whale_`m'\n",
    "    local small non_whale_`m'\n",
    "\n",
    "    * Small (non-whale) stats\n",
    "    quietly summarize `small', detail\n",
    "    local ms_mean = r(mean)\n",
    "    local ms_sd   = r(sd)\n",
    "    local ms_p10  = r(p10)\n",
    "    local ms_p50  = r(p50)\n",
    "    local ms_p90  = r(p90)\n",
    "\n",
    "    * Whale stats\n",
    "    quietly summarize `whale', detail\n",
    "    local mw_mean = r(mean)\n",
    "    local mw_sd   = r(sd)\n",
    "    local mw_p10  = r(p10)\n",
    "    local mw_p50  = r(p50)\n",
    "    local mw_p90  = r(p90)\n",
    "\n",
    "    * Difference in means with t-test\n",
    "    quietly gen __diff = `small' - `whale'\n",
    "\n",
    "    quietly reg __diff, vce(cluster space)\n",
    "\n",
    "    local d  = _b[_cons]\n",
    "    local tt = _b[_cons] / _se[_cons]\n",
    "    local p  = 2 * ttail(e(df_r), abs(`tt'))\n",
    "\n",
    "    drop __diff\n",
    "\n",
    "\n",
    "    * Stars\n",
    "    if (`p' < 0.01)      local stars \"***\"\n",
    "    else if (`p' < 0.05) local stars \"**\"\n",
    "    else if (`p' < 0.10) local stars \"*\"\n",
    "    else                 local stars \"\"\n",
    "\n",
    "    * Row label\n",
    "    if \"`m'\" == \"num_delegate\"         local label \"\\$\\textit{\\# Holders}_{i}\\$\"\n",
    "    else if \"`m'\" == \"turnout_delegate\"  local label \"\\$\\textit{Turnout}_{i}\\$\"\n",
    "    else if \"`m'\" == \"from_delegation_rate\"  local label \"\\$\\textit{Delegator}_{i}\\$\"\n",
    "    else if \"`m'\" == \"to_delegation_rate\"    local label \"\\$\\textit{Delegatee}_{i}\\$\"\n",
    "    else if \"`m'\" == \"reason_rate\"    local label \"\\$\\textit{Reason}_{i}\\$\"\n",
    "    else if \"`m'\" == \"vn_hhi\"    local label \"\\$\\textit{HHI}_{i}^{vn}\\$\"\n",
    "    else if \"`m'\" == \"timing_avg\"    local label \"\\$\\textit{Timing}_{i}\\$\"\n",
    "    else if \"`m'\" == \"user\"    local label \"\\$\\textit{User}_{i}\\$\"\n",
    "    else                      local label \"`m'\"\n",
    "\n",
    "    post H (\"`label'\") ///\n",
    "        (`ms_mean') (`ms_sd') (`ms_p10') (`ms_p50') (`ms_p90') ///\n",
    "        (`mw_mean') (`mw_sd') (`mw_p10') (`mw_p50') (`mw_p90') ///\n",
    "        (`d') (`tt') (`p') (\"`stars'\")\n",
    "}\n",
    "postclose H\n",
    "\n",
    "* -------------------- LATEX EXPORT --------------------\n",
    "use `results', clear\n",
    "\n",
    "tempname fh\n",
    "file open `fh' using \"$TABLES/sum_whale_non_whale.tex\", write replace\n",
    "\n",
    "file write `fh' \"{\\small\" _n\n",
    "file write `fh' \"\\begin{tabular}{lcccccccccccc}\" _n\n",
    "file write `fh' \"\\toprule\" _n\n",
    "file write `fh' \" & \\multicolumn{5}{c}{Small Shareholder (Holding \\$<\\$ 5\\%)} & \\multicolumn{5}{c}{Whale (Holding \\$\\ge\\$ 5\\%)} & \\multicolumn{2}{c}{Small - Whale} \\\\\" _n\n",
    "file write `fh' \"\\cmidrule(lr){2-6} \\cmidrule(lr){7-11} \\cmidrule(lr){12-13}\" _n\n",
    "file write `fh' \" & Mean & Std. Dev. & P10 & Median & P90 & Mean & Std. Dev. & P10 & Median & P90 & Diff. & t-stat \\\\\" _n\n",
    "file write `fh' \"\\midrule\" _n\n",
    "\n",
    "local Nobs = _N\n",
    "forvalues i = 1/`Nobs' {\n",
    "    local metric = metric[`i']\n",
    "\n",
    "    * Precision rules\n",
    "    if \"`metric'\" == \"\\$\\textit{\\# Holders}_{i}\\$\" {\n",
    "        local ms_mean_fmt : display %12.1f ms_mean[`i']\n",
    "        local ms_sd_fmt   : display %12.1f ms_sd[`i']\n",
    "        local ms_p10_fmt  : display %12.1f ms_p10[`i']\n",
    "        local ms_p50_fmt  : display %12.1f ms_p50[`i']\n",
    "        local ms_p90_fmt  : display %12.1f ms_p90[`i']\n",
    "\n",
    "        local mw_mean_fmt : display %12.1f mw_mean[`i']\n",
    "        local mw_sd_fmt   : display %12.1f mw_sd[`i']\n",
    "        local mw_p10_fmt  : display %12.1f mw_p10[`i']\n",
    "        local mw_p50_fmt  : display %12.1f mw_p50[`i']\n",
    "        local mw_p90_fmt  : display %12.1f mw_p90[`i']\n",
    "\n",
    "        local diff_fmt    : display %12.1f diff_mean[`i']\n",
    "        local tstat_fmt   : display %12.1f tstat[`i']\n",
    "    }\n",
    "    else {\n",
    "        * Fallback (3 dp)\n",
    "        local ms_mean_fmt : display %12.4f ms_mean[`i']\n",
    "        local ms_sd_fmt   : display %12.4f ms_sd[`i']\n",
    "        local ms_p10_fmt  : display %12.4f ms_p10[`i']\n",
    "        local ms_p50_fmt  : display %12.4f ms_p50[`i']\n",
    "        local ms_p90_fmt  : display %12.4f ms_p90[`i']\n",
    "\n",
    "        local mw_mean_fmt : display %12.4f mw_mean[`i']\n",
    "        local mw_sd_fmt   : display %12.4f mw_sd[`i']\n",
    "        local mw_p10_fmt  : display %12.4f mw_p10[`i']\n",
    "        local mw_p50_fmt  : display %12.4f mw_p50[`i']\n",
    "        local mw_p90_fmt  : display %12.4f mw_p90[`i']\n",
    "\n",
    "        local diff_fmt    : display %12.4f diff_mean[`i']\n",
    "        local tstat_fmt   : display %12.4f tstat[`i']\n",
    "    }\n",
    "\n",
    "    local st = stars[`i']\n",
    "    file write `fh' ///\n",
    "        \"`metric' & `ms_mean_fmt' & `ms_sd_fmt' & `ms_p10_fmt' & `ms_p50_fmt' & `ms_p90_fmt' & \" ///\n",
    "        \"`mw_mean_fmt' & `mw_sd_fmt' & `mw_p10_fmt' & `mw_p50_fmt' & `mw_p90_fmt' & \" ///\n",
    "        \"`diff_fmt' & `tstat_fmt'`st' \\\\\" _n\n",
    "}\n",
    "\n",
    "file write `fh' \"\\bottomrule\" _n \"\\end{tabular}}\" _n\n",
    "file close `fh'\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "venv",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.12.10"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
